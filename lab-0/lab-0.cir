* File: "lab-0.cir"
* Простейшая схема включения биполярного транзистора по схеме с общим
* эмиттером (ОЭ) в качестве усилителя переменного тока.
*
* Схема содержит два биполярных n-p-n транзистора работающих независимо.
* Эмиттеры транзисторов подключены к общей шине 0В.
* Коллекторы транзисторов подключены через резисторы RС1 и RC2
* номиналом 2.2 кОм к источнику питания +5В.
* По постоянному току база первого транзистора подключена к источнику
* питания +5В через резистор RB1.
* База второго транзистора подключена к его коллектору
* через резистор RB2, который образует обратную отрицательную связь (ООС)
* усилительного каскада на VT2.
* Каскад усилителя на VT1 не имеет термостабилизации.
* Каскад усилителя на VT2 имеет простейшую термостабилизацию в виде
* параллельной ООС.
* Через конденсаторы CIN1, CIN2 и резисторы RIN1 и RIN2 c источника
* синусоидального сигнала на базы транзисторов подается входной сигнал
* переменного тока.
* Через конденсаторы COUT1 и COUT2 к коллекторам транзисторов VT1 и VT2
* подключены нагрузки RL1 и RL2, напряжение на которых анализируется
* в процессе AC анализа.

* источник питания +5В
VCC vcc 0 DC 5V

* источник синусоидального сигнала
VSIN vsin 0 dc 0 ac 1mV sin(0 1mV 1kHz)

* входные конденсаторы
CIN1 vin1 vb1 10uF
CIN2 vin2 vb2 10uF

* выходные конденсаторы
COUT1 vvc1 vout1 10uF
COUT2 vvc2 vout2 10uF

* внутреннее сопротивление источника
RIN1 vsin vin1 1k
RIN2 vsin vin2 1k

* резисторы в базовых цепях транзисторов VT1 и VT2
* (обратите внимание, что RB1 подключен к +5В, а RB2 к коллектору VT2)
RB1 vcc  vvb1 540k
RB2 vvc2 vvb2 240k

* резистор-нагрузка к коллекторных цепях транзисторов VT1 и VT2
RC1 vcc vvc1 2.2k
RC2 vcc vvc2 2.2k

* активные нагрузки при работе по переменному току
RL1 vout1 0 1k
RL2 vout2 0 1k

* фиктивные источники напряжения 0В для контроля базовых токов VT1 и VT2
VB1 vvb1 vb1 0V
VB2 vvb2 vb2 0V

* фиктивные источники напряжения 0В для контроля коллекторных токов VT1 и VT2
VC1 vvc1 vc1 0V
VC2 vvc2 vc2 0V

* фиктивные источники напряжения 0В для контроля эмиттерных токов
VE1 ve1 0 0V
VE2 ve2 0 0V

* n-p-n транзисторы
QVT1 vc1 vb1 ve1 2N3904
QVT2 vc2 vb2 ve2 2N3904

* SPICE модель транзистора 2N3904 из примеров документации gEDA
.model 2N3904 NPN(Is=6.734f Xti=3 Eg=1.11 Vaf=74.03 Bf=416.4 Ne=1.259
+             Ise=6.734f Ikf=66.78m Xtb=1.5 Br=.7371 Nc=2 Isc=0 Ikr=0 Rc=1
+             Cjc=3.638p Mjc=.3085 Vjc=.75 Fc=.5 Cje=4.493p Mje=.2593 Vje=.75
+             Tr=239.5n Tf=301.2p Itf=.4 Vtf=4 Xtf=2 Rb=10)

* температурный режим
.options TEMP=27

* моделирование переходного процесса
* шаг моделирования - 1 мкс
* время моделирования - 10 мс
.tran 1u 10m

* AC анализ слабых сигналов при снятии АЧХ/ФЧХ
*.ac oct 3000 1Hz 100MegHz

* контроль рабочей точки по постоянному току:
* -> op
* -> print v(vb1) v(vc1) v(ve1)
* -> print v(vb2) v(vc2) v(ve2)
* -> print i(vb1) i(vc1) i(ve1)
* -> print i(vb2) i(vc2) i(ve2)

* симуляция переходного процесса
* (анализ переменных напряжений на входе и выходах
* -> run
* -> plot v(vsin) v(vout1) v(vout2)

* снятие АЧХ/ФЧХ (1Гц...100MHz)
* -> ac oct 3000 1Hz 100MegHz
* -> plot db(ac.v(vout1)/1m) db(ac.v(vout2)/1m)
* -> plot ph(ac.v(vout1))*360/(2*pi) ph(ac.v(vout2))*360/(2*pi)

.end

